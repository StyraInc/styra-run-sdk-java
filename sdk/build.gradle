plugins {
    id 'java'
    id 'groovy'
}

apply from: '../publish.gradle'

version '0.0.1-SNAPSHOT'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    implementation project(':styra-run-sdk-core')

    testImplementation 'org.apache.groovy:groovy:4.0.6'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'org.slf4j:slf4j-api:2.0.3'
    testImplementation('com.squareup.okhttp3:mockwebserver:4.10.0')
    testImplementation('com.squareup.okhttp3:okhttp-tls:4.10.0')
    testImplementation('com.google.code.tempus-fugit:tempus-fugit:1.1')
    testRuntimeOnly 'net.bytebuddy:byte-buddy:1.12.18'
    testRuntimeOnly 'org.objenesis:objenesis:3.3'
    testRuntimeClasspath 'ch.qos.logback:logback-classic:1.4.4'
}

test {
    useJUnitPlatform()
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                description = 'The Styra Run Java SDK'
            }
            from(components.java)
        }
    }
}
